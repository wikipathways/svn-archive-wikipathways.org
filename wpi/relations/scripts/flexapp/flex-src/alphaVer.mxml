<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" 
				xmlns:flex="flare.flex.*" layout="horizontal" 
				creationComplete="init()">
	<mx:Script source="wikipathways.as"/>
	<mx:Script>
		<![CDATA[
			import flare.animate.Transitioner;
			import flare.data.DataSet;
			import flare.data.DataSource;
			import flare.display.TextSprite;
			import flare.flex.FlareVis;
			import flare.physics.Simulation;
			import flare.util.Strings;
			import flare.vis.Visualization;
			import flare.vis.controls.*;
			import flare.vis.data.Data;
			import flare.vis.data.EdgeSprite;
			import flare.vis.data.NodeSprite;
			import flare.vis.events.SelectionEvent;
			import flare.vis.events.TooltipEvent;
			import flare.vis.operator.label.Labeler;
			import flare.vis.operator.layout.*;
			
			import flash.display.Sprite;
			import flash.events.MouseEvent;
			import flash.utils.flash_proxy;
			
			import mx.controls.Alert;
			import mx.controls.sliderClasses.Slider;
			import mx.core.UIComponent;
			import mx.events.SliderEvent;
			import mx.rpc.events.ResultEvent;
			import mx.rpc.http.HTTPService;
			
			
			private var vis:Visualization;
			private var visData:Data;
			private var dataSet:DataSet;
			private var panZoom:PanZoomWithResetControl;

			private var species:String = "mycobacterium tuberculosis";
			private var score = 0.8;
			private var relationType:String = "xref"; 
				
			[Bindable]
			private var speciesList:Array;
			
			// Layout data
			private var layouts:Array = [
				{label:"Circle", id:3},
				{label:"Force", id:2},
				{label:"Tree", id:1},				
				{label:"Radial", id:4}
			];
			
			private var relationTypeList:Array = [ "xref", "label"];
				
			private var currentLayoutId:int = 3;
			
			public function init():void
			{
				getSpecies();		
				loadData();
			}

			private function loadData():void
			{
				toggleUIControls(false);
				
				var ds:DataSource = new DataSource(
					"http://relations.wikipathways.org/wpi/relations/scripts/flexapp/?action=relations&species=" + species + "&type=" + relationType +  "&minscore=" + score , "graphml");
				var loader:URLLoader = ds.load();
				
				loader.addEventListener(Event.COMPLETE, function(evt:Event):void {
					
					dataSet = loader.data as DataSet;
					draw();
					setLayout(currentLayoutId);
					// update the visualization to draw everything
					vis.update();	
					// add the visualization to the flash movie
					uic.addChild(vis);
					scoreSlider.enabled = true;
				});
			}
			
			public function draw():void
			{	

				visData = Data.fromDataSet(dataSet);
				// create a visualization based on our data
				vis = new Visualization(visData);
				// Set the bounds
				vis.bounds = new Rectangle(uic.x , uic.y , 1000, 500);
				vis.x = 20;
				vis.y = 10;
				
				addLabel();																		
				
				// Manually change the color of all nodes
				vis.data.nodes.visit(function(n:NodeSprite):void
				{	
					n.fillColor = 0xf6eaea;
					n.buttonMode = true;
					n.fillAlpha = 1;
					n.size = 3;
					n.mouseChildren = false;
					n.doubleClickEnabled = true;
					n.addEventListener(MouseEvent.DOUBLE_CLICK, openPathwayPage);								
				});
				
				// Manually change the color of the edge
				vis.data.edges.visit(function(e:EdgeSprite):void
				{	
					e.lineColor = 0x2d2d2d;
					e.lineAlpha = 1;
					e.lineWidth = e.data.score * 5;
				});									
				
				toggleUIControls(true);
			}
			
			public function addControl():void
			{
				// Pan around + Zoom
				panZoom = new PanZoomWithResetControl(uic, 0.5, 2);
				vis.controls.add(panZoom);
				
				resetZoomButton.addEventListener(MouseEvent.CLICK, function(event:MouseEvent):void {
					panZoom.resetPanZoom(new Transitioner(2));
				});				
				
				vis.controls.add(new TooltipControl(NodeSprite, null,
					function(evt:TooltipEvent):void {
						TextSprite(evt.tooltip).text = Strings.format(
							"{data.name}", evt.node
						);
					}
				));
							
			}	

			private function addLabel():void
			{
				var format:TextFormat = new TextFormat();
				format.font = "Verdana";
				format.color = 0x000000;
				format.size = 9;
				format.align=TextFormatAlign.CENTER; 
				
				var labeler:Labeler = new Labeler("data.pwId");
				labeler.textMode = TextSprite.DEVICE;
				labeler.textFormat=format;
				vis.setOperator("labels", labeler);
				vis.update(null, "labels");
			}

			// Opens Pathway page in external window
			private function openPathwayPage(event:MouseEvent):void 
			{			
				var node:NodeSprite = event.target as NodeSprite;
//				mx.controls.Alert.show(node.data.category);
				var urlRequest:URLRequest = new URLRequest("http://www.relations.wikipathways.org/index.php/Pathway:" + node.data.pwId);
				navigateToURL(urlRequest, "_blank");
			}
			
			private function selectLayoutHandler(selectLayout:Event):void
			{
				vis.operators.removeOperatorAt(vis.operators.length - 1);
				setLayout(selectLayout.target.selectedItem.id);
			}

			private function selectRelationTypeHandler(selectType:Event):void
			{
				relationType = selectType.target.selectedItem;
				vis.data.clear();
				loadData();
			}
			private function selectSpeciesHandler(selectSpecies:Event):void
			{
				species = selectSpecies.target.selectedItem;
				vis.data.clear();
				loadData();
			}			
			
			private function setScoreHandler(setScore:SliderEvent):void
			{
				score = scoreSlider.value ;
				vis.data.clear();
				loadData();
			}
			
			private function toggleUIControls(enable:Boolean):void
			{
				if(enable)
				{
					resetZoomButton.enabled = true;
					scoreSlider.enabled = true;
					speciesSelect.enabled = true;
					selectLayout.enabled = true;
					typeSelect.enabled = true;
				}
				else
				{
					resetZoomButton.enabled = false;
					scoreSlider.enabled = false;
					speciesSelect.enabled = false;
					selectLayout.enabled = false;
					typeSelect.enabled = false;	
				}	
			}	
				
			private function setLayout(layoutId:int):void
			{
				
				vis.controls.clear();
				
				//lay.autoScale = false;
				//lay.fitToBounds = false;
				
				switch(layoutId)
				{
					case 1:
						vis.operators.add(new NodeLinkTreeLayout("topToBottom"));
						
						break;
					case 2:
						var forceLayout:ForceDirectedLayout = new ForceDirectedLayout();
						forceLayout.defaultSpringLength = 80;
						forceLayout.defaultParticleMass = 2;
						forceLayout.defaultSpringTension = 0.1;
						forceLayout.iterations = 10;
						forceLayout.ticksPerIteration = 1.5;
						vis.operators.add(forceLayout);
						vis.controls.add(new DragControl(NodeSprite));
						break;
					
					case 3:				
						var circleLayout:CircleLayout = new CircleLayout();
						circleLayout.startRadius = vis.data.nodes.length * 25;
						circleLayout.startRadiusFraction = 3/5;
						vis.operators.add(circleLayout);
						break;
					case 4:
						var radialLayout:RadialTreeLayout = new RadialTreeLayout();
						radialLayout.radiusIncrement = 50;
						radialLayout.autoScale = true;
						vis.operators.add(radialLayout);
						break;
				}
				
				vis.continuousUpdates = true;
				addControl();
				currentLayoutId = layoutId;
				var t:Transitioner = vis.update(4);
				t.play();				
				
			}	
		]]>
	</mx:Script>

	<mx:HBox height="500" width="750">
		<mx:Canvas id="container" width="100%" height="100%" borderStyle="outset" cornerRadius="30" borderColor="#8A8A8A">
			<mx:Canvas width="100%" height="100%" backgroundColor="0x000000" id="cv" />
			<flex:FlareVis id="tviz" width="100%" height="100%" mask="{cv}" hitArea="{container}">
				<mx:UIComponent id="uic" />
			</flex:FlareVis>			
		</mx:Canvas>
	</mx:HBox>	
	<mx:VBox height="500" width="200" borderStyle="solid" cornerRadius="15" borderColor="#0B0B0B" themeColor="#161616" alpha="1.0">
		<mx:Image source="images/wikipathways_logo.png" id="logo"/>
		<mx:Button id="resetZoomButton" label="Reset Zoom" enabled="false" />
		<mx:Label text="Relations Type" />
		<mx:ComboBox id="typeSelect" dataProvider="{relationTypeList}" change='selectRelationTypeHandler(event)' enabled="false" />
		<mx:Label text="Change Layout" />
		<mx:ComboBox id="selectLayout" dataProvider="{layouts}" change='selectLayoutHandler(event)'  enabled="false"/>
		<mx:Label text="Species" />
		<mx:ComboBox id="speciesSelect" dataProvider="{speciesList}" change='selectSpeciesHandler(event)'  enabled="false"/>
		<mx:Label text="Minimum Score" />
		<mx:HSlider id="scoreSlider" change="setScoreHandler(event)" 
					minimum="0" maximum="1"
					value="0.7" 
					labels="['0%','100%']" 
					snapInterval="0.05"
					enabled="false"
					/>
	</mx:VBox>
</mx:Application>
